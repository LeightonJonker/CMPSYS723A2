module cruiseSpeedSetter:

constant SpeedInc = 2.5f : float;
constant minSpeed = 30.0f : float;
constant maxSpeed = 150.0f : float;

input cruiseState := 1 : integer; %1 = OFF, 2 = ON, 3 = STDBY, 4 = DISABLE
input Set;
input QuickAccel;
input QuickDecel;
input csExceedMin;
input csExceedMax;
input speed : float;
output cruiseSpeed : float;

loop
    await cruiseState;
    if not (?cruiseState = 1) then
        emit cruiseSpeed(?speed);
        loop
            await cruiseState;
            if not (?cruiseState = 1) then
                
                present csExceedMin then
                    emit cruiseSpeed(minSpeed);
                end present;

                present csExceedMax and not csExceedMin then
                    emit cruiseSpeed(maxSpeed);
                end present;
                
                present Set and not (csExceedMin or csExceedMax) then
                    emit cruiseSpeed(?speed);
                end present;
                
                present QuickDecel and not (csExceedMin or csExceedMax or Set) then
                    emit cruiseSpeed(pre(?cruiseSpeed) - SpeedInc);
                end present;

                present QuickAccel and not (csExceedMin or csExceedMax or Set or QuickDecel) then
                    emit cruiseSpeed(pre(?cruiseSpeed) + SpeedInc);
                end present;
                
            end if;
        end loop;
    end if;
end loop;
end module
